#!/usr/bin/env perl
use v5.18;
use strict;
use warnings;

use Encode qw(encode_utf8);

use FindBin;
use lib $FindBin::Bin . "/lib";
use Fun::File qw(srl slurp);

my %args = @ARGV;
my $input_dir   = $args{'-i'} or die "No input dir -i";

my @sources = glob($input_dir . "/twitter-status-*.srl");

my %seen;
for my $f (@sources) {
    my $rows = srl(slurp($f));
    for my $tweet (@$rows) {
        next if $seen{$tweet->{id}};
        $seen{$tweet->{id}} = 1;

        my $txt = $tweet->{full_text} // $tweet->{text};
        $txt =~ s/\n/\n    /g;

        if ($tweet->{entities} && $tweet->{entities}{urls} && @{$tweet->{entities}{urls}}) {
            $txt .= "\n    \n";
            my $i = 0;
            for my $url (map { $_->{expanded_url} } @{$tweet->{entities}{urls}}) {
                $i++;
                $txt .= "    [url-$i]: $url\n";
            }
            $txt .= "\n";
        }

        say encode_utf8($tweet->{"user.id"} . ":\n\t" . $txt . "\n");
    }
}
