#!/usr/bin/env perl
use v5.18;
use strict;
use warnings;

use File::Next;
use Getopt::Long;
use Digest::SHA1;
use File::Touch;
use Image::ExifTool qw(:Public);
use Time::Moment;

sub main {
    my ($opts, $args) = @_;
    my @input = grep { -f || -d } @{ $opts->{i} };
    my $iter = File::Next::files(@input);
    my %plan;
    my $local_offset = Time::Moment->now->offset;
    while (defined( my $file = $iter->() )) {
        next unless $file =~ / \.(jpg|heic) \z/xis;

        my $info = ImageInfo($file, 'DateTimeOriginal');
        my $dto = $info->{DateTimeOriginal};
        if (! $dto) {
            say "SKIP: no DateTimeOriginal\t$file";
            next;
        }
        next unless $dto =~ /\A [12][0-9]{3}:[0-9]{2}:[0-9]{2} \s [0-9]{2}:[0-9]{2}:[0-9]{2} \z/x;
        my @exif_dto = split /[ :]/, $dto;
        my $tm_dto = Time::Moment->new(
            year       => $exif_dto[0],
            month      => $exif_dto[1],
            day        => $exif_dto[2],
            hour       => $exif_dto[3],
            minute     => $exif_dto[4],
            second     => $exif_dto[5],
            # nanosecond => 0,
            offset     => $local_offset,
        );

        my $mtime = (stat($file))[9];
        my $tm_mtime = Time::Moment->from_epoch($mtime);
        if ($tm_mtime != $tm_dto) {
            say "$tm_dto\t$file";
            if ($opts->{n}) {
                say "DRY RUN: $file => $tm_dto";
            } else {
                File::Touch->new( mtime => $tm_dto->epoch )->touch($file);
            }
        } else {
            say "SKIP: DateTimeOriginal == mtime == $tm_dto\t$file";
        }
    }
}

my %opts;
GetOptions(
    \%opts,
    'i=s@',
    'd=s',
    'n',
    'ignore-existing'
);
main(\%opts, [@ARGV]);

