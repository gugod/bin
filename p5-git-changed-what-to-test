#!/usr/bin/env perl
use v5.18;
use strict;
use warnings;
use File::Spec;
use Getopt::Long qw(GetOptions);
use JSON::PP qw(decode_json);
use File::Next;
use Module::ExtractUse;
use Module::Path ();

sub module_path {
    my ($mod, $libs) = @_;
    local @INC = @$libs;
    return Module::Path::module_path($mod);
}

sub grok_changed {
    my @changed;
    open(my $fh, 'git status --porcelain . |');
    while (<$fh>) {
        chomp;
        if (s/\A[ AM][ AM] //) {
            push @changed, $_;
        }
    }
    close($fh);
    if (@changed == 0) {
        open(my $fh, 'git log -1 --format= --name-status . |');
        while (<$fh>) {
            chomp;
            if (s/\A[AM]\s+//) {
                push @changed, $_;
            }
        }
        close($fh);
    }
    return @changed;
}

sub slurp_json {
    my ($fn) = @_;
    open(my $fh, "<:utf8", $fn) or die $!;
    local $/;
    my $o = decode_json(scalar <$fh>);
    close($fh);
    return $o;
}

sub any(&@) {
    my $cb = shift;
    for (@_) {
        return 1 if $cb->();
    }
    return 0;
}

my %opts;
Getopt::Long::Configure('no_ignore_case');
GetOptions(
    \%opts,
    "r=s",
    "l=s@",
    "L=s@",
);

my @changed = map { File::Spec->rel2abs($_) } grok_changed();

unless (@changed) {
    exit(0);
}

if ($opts{r}) {
    my $rdeps = slurp_json($opts{r});
    my %bechanged;
    for (@changed) {
        my @o = @{$rdeps->{$_} //[$_]};
        @bechanged{@o} = ();
    }
    say for grep { /\.t/ } keys %bechanged;
} else {
    my @perl5libs;
    if ($opts{L}) {
        @perl5libs = @{$opts{L}};
    } elsif ($opts{l}) {
        @perl5libs = (@{$opts{l}}, @INC);
    } else {
        @perl5libs = @INC;
    }

    my %mod_path;
    my $tests = File::Next::files({ file_filter => sub { /\.t\z/ }}, "t");
    my %is_changed = map { $_ => 1 } @changed;

    while (defined(my $testfile = $tests->())) {
        my $meu = Module::ExtractUse->new;
        $meu->extract_use($testfile);
        my @deps = grep { $_ } map { $mod_path{$_} //= module_path($_, \@perl5libs) } $meu->array;
        if (any { $is_changed{$_} } @deps) {
            say $testfile;
        }
    }
}
