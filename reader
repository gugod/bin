#!/usr/bin/env perl
use v5.14;
use File::Basename qw(basename);

use JSON;
use Encode 'encode_utf8';
use NewsExtractor;

$SIG{__DIE__} = sub {
    require Devel::StackTrace;
    print Devel::StackTrace->new->as_string;
    exit;
};

my $program_name = basename($0);

my $url = shift @ARGV or die <<USAGE;

Pass URLs from argv:

   $program_name URL

USAGE

sub extract_as_text {
    my $url = shift;

    my ($error, $article) = NewsExtractor->new( url => $url )->download->parse;

    my $json = JSON->new->pretty->canonical->utf8;
    if ($error) {
	die $json->encode({
	    url => $url,
	    DownloadFailure => { message => $error->message, debug => $error->debug }});
    }

    return join(
        "\n",
        "Headline: " . $article->headline,
        "Dateline: " . ($article->dateline || "(unknown)"),
        "Journalist: " . ($article->journalist || "(unknown)"),
        "\n",
        $article->article_body,
    );
}

say encode_utf8( extract_as_text($url) );
