#!/usr/bin/env perl

=head1 SYNOPSIS

C<exif-search> is a program that finds files with matching EXIF information.

Example: Find files store under 2 folders with Geo tags that is with 5kms from [Erlin Butokuden][1]

    exif-search \
        --ll 23.8997913,120.3691942 --disntance 5 \
        /data/main/Pictures /data/backup/Pictures

[1]: https://goo.gl/maps/genakeXbVdaK8oDb7

=head1 OPTIONS

    --ll        lat,long -- that is, 2 decimals with a comma in between.
                This is the usual format found in valious web-based maps.

    --distance  A decimal number in kilometres. The default is 10.
                This option is only meaningful if C<--ll> is also given.

=cut

use v5.18;
use strict;
use warnings;

use Pod::Usage qw(pod2usage);
use GIS::Distance;
use File::Next;
use Getopt::Long;
use Image::ExifTool;
use Image::ExifTool::Location;

sub main {
    my ($opts, $args) = @_;
    my (@paths) = @$args;

    pod2usage({ -verbose => 2 }) if $opts->{help};
    pod2usage({ -verbose => 1 }) unless @paths && $opts->{ll} && $opts->{distance};

    my $distance = $opts->{distance};
    my ($lat, $long) = split(',', $opts->{ll}, 2);
    pod2usage({ -verbose => 1 })  unless defined($lat) && defined($long);

    my $iter = File::Next::files(+{ file_filter => sub { /\.jpg\z/i } }, @paths );
    my $gis = GIS::Distance->new();

    while (my $file = $iter->()) {
        my $exif = Image::ExifTool->new;
        $exif->ExtractInfo($file);
        next unless $exif->HasLocation;

        my ($image_lat, $image_long) = $exif->GetLocation;
        my $d = $gis->distance($lat, $long, $image_lat, $image_long)->kilometre;

        # say "$file\t$d";

        next unless $d < $distance;

        say "$file\t$d";
    }
}

my %opts;
GetOptions(
    \%opts,
    'help|h',
    'll=s',
    'distance=n',
) || pod2usage({ -verbose => 1 });
main(\%opts, [@ARGV]);
