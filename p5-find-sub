#!/usr/bin/env perl

use v5.18;
use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/lib";
use Fun 'foreach_perl_source_file';

use Getopt::Long 'GetOptions';
use PPI;
use PPI::Document;

my ($wanted_sub_name, @paths) = @ARGV;

unless ($wanted_sub_name) {
    die <<USAGE;
p5-find-sub name [path1] [path2]...
USAGE
}

@paths = ('.') unless @paths;

foreach_perl_source_file \@paths => sub {
    my ($file) = @_;
    my $doc = PPI::Document->new($file) or return;

    my $o = $doc->find(
        sub {
            my $el = $_[1];
            return (
                $el->isa("PPI::Token::Word") && $el eq "sub" && (
                    $el->parent->isa("PPI::Statement::Sub") ||
                    $el->snext_sibling()->isa("PPI::Structure::Block")
                )
            );
        }
    ) or return;

    for my $token (@$o) {
        # $token is the word "sub"
        my $el = $token->parent;
        my $sub_name = $el->isa("PPI::Statement::Sub") ? $el->name : "(anonymous)";
        if ($wanted_sub_name eq $sub_name) {
            say join ":", $file, $token->line_number, $sub_name;
        }
    }
};
