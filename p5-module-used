#!/usr/bin/env perl
# -*- cperl -*-
use v5.10;
use strict;
use warnings;
use File::Spec;
use File::Find;
use Module::CoreList;
use Module::ExtractUse;
use List::MoreUtils qw(uniq);
use Getopt::Long qw(GetOptions);


GetOptions(
    "cpanfile" => (\my $generate_cpanfile),
);

my @dir = map { File::Spec->rel2abs($_) } @ARGV;
push @dir, File::Spec->rel2abs(File::Spec->curdir) unless @dir;

my $meu = Module::ExtractUse->new;

my @deps;
find(
    sub {
        return if $File::Find::dir =~ /\.(git|sv[nk])/ || ! -f $File::Find::name;
        # say STDERR '<<< ' . $File::Find::name;
        $meu->extract_use($File::Find::name);
        push @deps, grep { !$Module::CoreList::version{"$]"}->{$_}  } grep { !/^v?[\.0-9]+$/ } uniq sort $meu->array;
    },
    @dir
);

if ($generate_cpanfile) {
    for (uniq(@deps)) {
        say "requires \"$_\" => 0;";
    }
} else {
    say for @deps;
}
