#!/usr/bin/env perl

use v5.18;
use strict;
use warnings;

use File::Spec;
use File::Next;
use PPI::Document;
use Scalar::Util qw(refaddr);

use constant OP_PRIORITY => do {
    # perldoc perlop

    my %priority;
    my $p = 100;
    $priority{$_} = $p for ('++', '--');
    $p--;

    $priority{$_} = $p for ('**');
    $p--;

    $priority{$_} = $p for ('<', '>', '<=', '>=', 'lt', 'gt', 'le', 'ge');
    $p--;

    $priority{$_} = $p for ('==', '!=', '<=>', 'eq', 'ne', 'cmp', '~~');
    $p--;

    $priority{$_} = $p for ('&&');
    $p--;

    $priority{$_} = $p for ('//', '||');
    $p--;

    \%priority
};

sub sayit {
    my ($file, $found) = @_;

    say "# " . $file . ":" . $found->[0]->line_number;
    say $found->[0]->parent->content;
    say "Operators: " . join " ... ", map { $_->content . '('. OP_PRIORITY->{$_->content} . ')' } @$found;
 
   say "";
}

sub search_and_say {
    my ($file) = @_;

    my $doc = PPI::Document->new($file, readonly => 1);
    $doc->index_locations;

    my $tokens = $doc->find(
        sub {
            my ($el) = $_[1];
            return 0 unless $el->significant && $el->isa("PPI::Token::Operator");
            my $op = $el->content();
            return !!(OP_PRIORITY->{$op});
        }
    ) or return;

    my @found;
    for (@$tokens) {
        if (@found) {
            if ( refaddr($found[0]->parent) == refaddr($_->parent) ) {
                push @found, $_;
            } else {
                sayit($file, \@found) if @found > 1;
                @found = ();
            }
        } else {
            push @found, $_;
        }
    }
    sayit($file, \@found) if @found > 1;
}

my $files = File::Next::files({
    file_filter => sub {
        return 1 if /\.p[ml]\z/i;
        return 0 if !/\A\./i && /\./i;

        my $file = $_;
        if (open my $fh, '<', $file) {
            my $line = <$fh>;
            return 1 if $line =~ m{^#!.*perl};
        }
        return 0;
    }
}, $ARGV[0] // '.');

while (defined( my $file = $files->())) {
    search_and_say($file);
}

$a = rand();

say "meh" unless $a // 0 > 0.5;
