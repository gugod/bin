#!/usr/bin/env perl

use v5.14;
use Mojo::UserAgent;
use URI;
use Getopt::Long qw(GetOptions);

use constant IS_A_FEED_MIME_TYPE => { map { $_ => 1 } (
    'application/x.atom+xml',
    'application/atom+xml',
    'application/xml',
    'text/xml',
    'application/rss+xml',
    'application/rdf+xml',
)};


my %opts;
GetOptions(
    \%opts,
    "deep"
);

my @urls = @ARGV;

die "Put urls in ARGV\n"
    unless @urls;

my $ua = Mojo::UserAgent->new->max_redirects(5);
for my $base_url (@urls) {
    my $tx = $ua->get($base_url);
    my $dom = $tx->res->dom;
    my @feeds = (
        map {
            URI->new_abs($_, $base_url)->as_string;
        }
        grep {
            defined($_) && ($_ ne '')
        }
        @{ $dom->find("link[rel='alternate'],link[rel='service.feed']")->map(
            sub {
                my $type = $_->attr('type');
                return unless $type && IS_A_FEED_MIME_TYPE->{ $type };
                return $_->attr('href');
            }
        ) }
    );

    $dom->find("a[href]")->each(
        sub {
            my $href =  $_->attr('href');
            if ($href =~ m{\A https://feeds\.feedburner\.com\/ }x) {
                push @feeds, $href;
            }
        }
    );

    if ($opts{deep}) {
        $dom->find("a[href]")->each(
            sub {
                my $href =  $_->attr('href');
                my $url = URI->new_abs($href, $base_url)->as_string;
                my $ct = $ua->get($url)->res->headers->content_type;
                $ct =~ s/;\s+charset=.+$//;

                if ( IS_A_FEED_MIME_TYPE->{$ct} ) {
                    push @feeds, $url;
                }
            }
        );
    }
    say for @feeds;
}
