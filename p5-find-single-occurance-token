#!/usr/bin/env perl

use v5.18;
use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/lib";
use Fun 'foreach_perl_source_file';

use PPI;
use PPI::Document;
use Data::Dumper;
use Devel::Peek;

my %idx;

my @args = @ARGV;
@args = (".") if !@args;
foreach_perl_source_file \@args => sub {
    my ($file) = @_;
    say "... $file";
    my $doc = PPI::Document->new($file) or return;
    $doc->index_locations;

    my $o = $doc->find(
        sub {
            $_[1]->isa("PPI::Token::Word")
        }
    ) or return;

    for my $el (@$o) {
        my $s = "" . $el;
        $idx{$s}{frequency} += 1;
        $idx{$s}{WHERE} //= { file => $file, line_number => $el->line_number, column_number => $el->column_number };
    }
};

say for map {
    my $w = $idx{$_}{WHERE};
    $w->{file} . ':' . $w->{line_number} . ':' . $w->{column_number} . ': q' . $_;
}
sort {
    $idx{$a}{WHERE}{file} cmp $idx{$b}{WHERE}{file};
}
grep {
    $idx{$_}{frequency} == 1
} keys %idx;
