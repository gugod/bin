#!/usr/bin/env perl
use v5.18;

use Getopt::Long;

use Encode qw(encode_utf8);
use Sereal::Decoder;

my %args;
GetOptions(
    \%args,
    "i=s",
    "group=s",
    "group_order_by=s",
);

$args{group} //= "user.id";
$args{group_order_by} //= "length";

my $input_dir = $args{i} or die "No input dir: -i <input_dir>";

sub t2d {
    my ($t) = @_;
    my ($dd,$mm,$yyyy) = (localtime($t))[3,4,5];
    return sprintf('%04d-%02d-%02d', ($yyyy+1900), ($mm+1), $dd);
}

my $srl_decoder = Sereal::Decoder->new;

my $yesterday = t2d(time - 86400);
my $f = "${input_dir}/twitter-timeline-daily-${yesterday}.srl";

unless (-f $f) {
    say "Cannot find tweets from yesterday: $f";
    exit(0);
}

my $content = do {
    local $/ = undef;
    open my $fh, "<", $f;
    <$fh>;
};

my $mail_body;

my $tweets = $srl_decoder->decode($content);

my %group;

if (defined(my $group_field = $args{group})) {
    for my $tweet (@$tweets) {
        my $g = $tweet->{$group_field};
        push @{$group{$g}}, $tweet;
    }
} else {
    for my $tweet (@$tweets) {
        my @t = localtime($tweet->{created_at});
        my $g = $t[2];          # hour of the day
        push @{$group{$g}}, $tweet;
    }
}

sub group_sorted_by {
    my ($sorter, $groups) = @_;
    my $sub = main->can("group_sorted_by_${sorter}") or die "Unknown sorter: $sorter";
    return $sub->($groups);
}

sub group_sorted_by_name {
    my $groups = shift;
    return sort { $a cmp $b } keys %$groups;
}

sub group_sorted_by_length {
    my $groups = shift;
    return sort { @{$groups->{$a}} <=> @{$groups->{$b}} } keys %$groups;
}

for my $group_name (group_sorted_by($args{"group_order_by"}, \%group)) {
    $mail_body .= "# $group_name\n\n";

    for my $tweet (sort { $a->{created_at} <=> $b->{created_at} } @{$group{$group_name}}) {
        my $txt = ($tweet->{text} =~ s/\n/\n  /grs);
        $txt =~ s/\n+\z//;

        if ( length($txt) > 70 || index($txt,"\n") >= 0 ) {
            $txt .= "\n";
        }
        $mail_body .= "- $txt\n";
    }
    $mail_body .= "\n";
}

say encode_utf8($mail_body);
