#!/usr/bin/env perl
use strict;
use warnings;
use Encode qw(decode_utf8);
use LWP::UserAgent;
use Mojo::DOM;

my ($url, $selector) = @ARGV;
die "Usage: $0 url css-selecotr" unless $url && $selector;

binmode STDOUT, ":utf8";

my $content;
if ($url eq "-") {
    local $/ = undef;
    $content = decode_utf8(<STDIN>);
} else {
    my $ua = LWP::UserAgent->new;
    my $response = $ua->get($url);

    die "Failed to get the URL" unless $response->is_success;
    die "The response is not HTML" unless $response->header("Content-Type") =~ m{^text/html\s*(;|$)};

    $content = $response->decoded_content;
}

my $dom = Mojo::DOM->new;
$dom->parse($content);

$dom->find($selector)->each(
    sub {
        my $el = shift;
        my $attrs = $el->attr;
        print $el->type . ":\n";
        for (keys %$attrs) {
            print "  " . $_ . ": " . $attrs->{$_} . "\n";
        }
        print "  # " . $el->text;

        print "\n\n";
});
