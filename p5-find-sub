#!/usr/bin/env perl

use v5.18;
use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/lib";
use Fun 'foreach_perl_source_file';

use Getopt::Long 'GetOptions';
use PPI;
use PPI::Document;


my %opt;
GetOptions(
    \%opt,
    "--name=s",
);

my @args = @ARGV;
@args = (".") if !@args;
foreach_perl_source_file \@args => sub {
    my ($file) = @_;
    my $doc = PPI::Document->new($file) or return;

    my $o = $doc->find(
        sub {
            my $el = $_[1];
            return (
                $el->isa("PPI::Token::Word") && $el eq "sub" && (
                    $el->parent->isa("PPI::Statement::Sub") ||
                    $el->snext_sibling()->isa("PPI::Structure::Block")
                )
            );
        }
    ) or return;

    for my $token (@$o) {
        # $token is the word "sub"
        my $el = $token->parent;
        my $sub_name = $el->isa("PPI::Statement::Sub") ? $el->name : "(anonymous)";
        if (!$opt{name} || $opt{name} eq $sub_name) {
            say join ":", $file, $token->line_number, $sub_name;
        }
    }
};
